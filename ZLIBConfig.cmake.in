@PACKAGE_INIT@

include(${CMAKE_CURRENT_LIST_DIR}/ZLIBConfigInternal.cmake)

add_library(ZLIB::ZLIB INTERFACE IMPORTED)
set_target_properties(ZLIB::ZLIB PROPERTIES INTERFACE_LINK_LIBRARIES zlib_zlib)

get_target_property(ZLIB_INCLUDE_DIRS zlib_zlib INTERFACE_INCLUDE_DIRECTORIES)
set(ZLIB_INCLUDE_DIR ${ZLIB_INCLUDE_DIRS})
set(ZLIB_LIBRARIES ZLIB::ZLIB)
set(ZLIB_LIBRARY ${ZLIB_LIBRARIES})

string(REGEX REPLACE "^([0-9]+).*$" "\\1" ZLIB_VERSION_MAJOR "@ZLIB_FULL_VERSION@")
string(REGEX REPLACE "^[0-9]+\\.([0-9]+).*$" "\\1" ZLIB_VERSION_MINOR  "@ZLIB_FULL_VERSION@")
string(REGEX REPLACE "^[0-9]+\\.[0-9]+\\.([0-9]+).*$" "\\1" ZLIB_VERSION_PATCH "@ZLIB_FULL_VERSION@")
set(ZLIB_VERSION_STRING "${ZLIB_VERSION_MAJOR}.${ZLIB_VERSION_MINOR}.${ZLIB_VERSION_PATCH}")

# only append a TWEAK version if it exists:
set(ZLIB_VERSION_TWEAK "")
if( "@ZLIB_FULL_VERSION@" MATCHES "^[0-9]+\\.[0-9]+\\.[0-9]+\\.([0-9]+).*$")
    set(ZLIB_VERSION_TWEAK "${CMAKE_MATCH_1}")
    string(APPEND ZLIB_VERSION_STRING ".${ZLIB_VERSION_TWEAK}")
endif()

set(ZLIB_MAJOR_VERSION "${ZLIB_VERSION_MAJOR}")
set(ZLIB_MINOR_VERSION "${ZLIB_VERSION_MINOR}")
set(ZLIB_PATCH_VERSION "${ZLIB_VERSION_PATCH}")
